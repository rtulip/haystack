include "std/assert.hay"
struct Vec<T> {
    Arr<T>: slice
    u64: len
impl:

    inline fn Vec.from_parts<T>(Arr<T> u64) -> [Vec<T>] {
        cast(Vec)
    }

    inline fn Vec.new<T>() -> [Vec<T>] {
        8 malloc::<T> 0 cast(Vec) 
    }

    inline fn Vec.with_capacity<T>(u64) -> [Vec<T>] {
        malloc::<T> 0 cast(Vec)
    }

    inline fn Vec.delete<T>(&Vec<T>: self) {
        self::slice @ free
    }

    inline fn Vec.is_empty<T>(&Vec<T>: self) -> [bool] {
        self::len @ 0 ==
    }

    inline fn Vec.slice_mut<T>(*Vec<T>: self) -> [Arr<T>] {
        self::slice @
    }

    inline fn Vec.slice<T>(&Vec<T>: self) -> [ConstArr<T>] {
        self::slice::size @
        self::slice::data @
        cast(ConstArr)
    }

    // Adds an element to the end of the vector.
    //
    // Inputs:
    // - `T (value)`: the value to add to the vector
    // - `*Vec<T> (self)`: a mutable reference to the vector to add the value to
    //
    // Output:
    // - This function returns `()` and does not allocate a new vector.
    //
    // Example:
    // ```
    // Vec.new::<u64> as [mut v]
    // 12345 *v Vec.push
    // 0 &v Vec.at Option.unwrap 12345 == assert
    // ```
    fn Vec.push<T>(T: value *Vec<T>: self) {
        self::len @ 0 == if {
            2 self::slice @ realloc self::slice ! 
        } else self::len @ self::slice::size @ == if {
            self::slice::size @ 2 * self::slice @ realloc self::slice !
        }
        value self::len @ self::slice @ Arr.set
        self::len @ 1 + self::len !
    }

    // Pops the last element off the vector and return it inside of an `Option`, 
    // or return `Option.None` if the vector is empty.
    //
    // Inputs:
    // - `*Vec<T> (self)`: a mutable reference to the vector to pop from
    //
    // Output:
    // - An `Option` containing the popped element, or `Option.None` if the vector is empty.
    //
    // Example:
    // ```
    // Vec.new::<u64> as [mut v]
    // 12345 *v Vec.push
    // 12345 v Vec.pop Option.unwrap 12345 == assert
    // ```
    fn Vec.pop<T>(*Vec<T>: self) -> [Option<T>] {
        self::len @ 0 == if {
            Option.None::<T>
        } else {
            self::len @ 1 - self::len !
            self::len @ self::slice @ Arr.get Option.Some
        }
    }

    // Get a reference to the element at the given index in the vector, 
    // or return `Option.None` if the index is out of bounds.
    //
    // Inputs:
    // - `u64 (idx)`: the index of the element to get
    // - `&Vec<T> (self)`: a reference to the vector to get the element from
    //
    // Output:
    // - An `Option` containing a reference to the requested element, 
    //   or `Option.None` if the index is out of bounds.
    //
    // Example:
    // ```
    // Vec.new::<u64> as [mut v]
    // 10 *v Vec.push
    // 20 *v Vec.push
    // 30 *v Vec.push
    // 40 *v Vec.push
    // 0 &v Vec.get Option.unwrap == 10 assert
    // 3 &v Vec.get Option.unwrap == 40 assert
    // 5 &v Vec.get Option.unwrap Option.None == assert
    // ```
    fn Vec.get<T>(u64: idx &Vec<T>: self) -> [Option<&T>] {
        idx self::len @ >= if {
            Option.None::<&T>
        } else {
            idx self::slice @ Arr.get_ref Option.Some
        }
    }

    // Get a mutable reference to the element at the given index in the vector, or return `Option.None` if the index is out of bounds.
    //
    // Inputs:
    // - `u64 (idx)`: the index of the element to get
    // - `*Vec<T> (self)`: a mutable reference to the vector to get the element from
    //
    // Output:
    // - An `Option` containing a mutable pointer to the requested element, or `Option.None` if the index is out of bounds.
    //
    // Example:
    // ```
    // Vec.new::<u64> as [mut v]
    // 12345 *v Vec.push
    // *v 0 Vec.get_mut Option.unwrap 12345 == assert
    // ```
    fn Vec.get_mut<T>(u64: idx *Vec<T>: self) -> [Option<*T>] {
        idx self::len @ >= if {
            Option.None::<*T>
        } else {
            idx self::slice @ Arr.get_ref_mut Option.Some
        }
    }

    // Return an `Option` containing a reference to the element at the specified index in the vector, or `Option.None` if the index is out of bounds.
    //
    // Inputs:
    // - `idx`: the index of the element to retrieve
    // - `&Vec<T> (self)`: a reference to the vector to retrieve the element from
    //
    // Output:
    // - An `Option` containing a reference to the requested element, or `Option.None` if the index is out of bounds.
    //
    // Example:
    // ```
    // Vec.new::<u64> as [mut v]
    // 12345 *v Vec.push
    // 0 &v Vec.at Option.unwrap 12345 == assert
    // 1 &v Vec.at Option.None == assert
    // ```
    fn Vec.at<T>(u64: idx &Vec<T>: self) -> [Option<T>] {
        idx self::len @ >= if {
            Option.None::<T>
        } else {
            idx self::slice @ Arr.get Option.Some
        }
    }

    // Return an `Option` containing a reference to the last element in the vector, 
    // or `Option.None` if the vector is empty.
    //
    // Inputs:
    // - `&Vec<T> (self)`: a reference to the vector to retrieve the last element from
    //
    // Output:
    // - An `Option` containing a reference to the last element in the vector, 
    // or `Option.None` if the vector is empty.
    //
    // Example:
    // ```
    // Vec.new::<u64> as [mut v]
    // 123 *v Vec.push
    // 456 *v Vec.push
    // &v Vec.last Option.unwrap 456 == assert
    // ```
    inline fn Vec.last<T>(&Vec<T>: self) -> [Option<&T>] {
        self::len @ 1- self Vec.get
    }

    // Return the number of elements that the vector can hold without reallocating its underlying storage.
    //
    // Inputs:
    // - `self`: a reference to the vector to retrieve the capacity of
    //
    // Output:
    // - The number of elements that the vector can hold without reallocating its underlying storage.
    //
    // Example:
    // ```
    // not sure how to do this one :(
    // ```
    fn Vec.capacity<T>(&Vec<T>: self) -> [u64] {
        self::slice::size @
    }

    // Return the number of elements in the vector.
    //
    // Inputs:
    // - `self`: a reference to the vector to retrieve the length of
    //
    // Output:
    // - The number of elements in the vector.
    //
    // Example:
    // ```
    // Vec.new::<u64> as [mut v]
    // 123 *v Vec.push
    // 456 *v Vec.push
    // &v Vec.len 2 == assert
    // ```
    fn Vec.len<T>(&Vec<T>: self) -> [u64] {
        self::len @
    }

    // Check if the vector contains a given element.
    //
    // Inputs:
    // - `item`: the element to search for
    // - `self`: a reference to the vector to search in
    //
    // Output:
    // - `true` if the vector contains the given element, `false` otherwise.
    //
    // Example:
    // ```
    // [1 2 3 4] 3 Vec.contains true == assert
    // [1 2 3 4] 5 Vec.contains false == assert
    // ```
    fn Vec.contains<T>(T: item &Vec<T>: self) -> [bool] {

        0 while dup self Vec.len < do {
            as [i]
            i self Vec.at Option.unwrap item == if {
                true return
            }
            i 1 +
        } drop

        false

    }

    // Append the elements of another vector to this vector.
    //
    // Inputs:
    // - `other`: a mutable reference to the vector whose elements to append to `self`.
    // - `self`: a mutable reference to the vector to append the elements to.
    //
    // Output:
    // - This function returns `()` and does not allocate a new vector.
    //
    // Example:
    // ```
    // Vec.new::<u64> as [mut v]
    // Vec.new::<u64> as [mut w]
    // 123 *v Vec.push
    // 456 *w Vec.push
    // *w *v Vec.append Vec.last Option.unwrap 456 == assert
    // ```
    fn Vec.append<T>(*Vec<T>: other *Vec<T>: self) {
        Vec.new::<T> as [mut temp]

        while other Vec.is_empty lnot do {
            other Vec.pop Option.unwrap *temp Vec.push
        }

        while &temp Vec.is_empty lnot do {
            *temp Vec.pop Option.unwrap self Vec.push
        }

        &temp Vec.delete

    }
    
    // Reverse the elements of the vector in place.
    //
    // Inputs:
    // - `self`: a mutable reference to the vector to reverse.
    //
    // Output:
    // - This function returns a new vector with the reversed elements.
    //
    // Example:
    // ```
    // Vec.new::<u64> as [mut v]
    // 123 *v Vec.push
    // 456 *v Vec.push
    // &v Vec.last Option.unwrap 123 == assert
    // ```
    fn Vec.reverse<T>(Vec<T>: mut self) -> [Vec<T>] {

        Vec.new::<T> as [mut rev]
        while &self Vec.is_empty lnot do {
            *self Vec.pop Option.unwrap *rev Vec.push
        }

        &self Vec.delete
        rev

    }
}

impl<T> Format<&Vec<T>>
requires: [Format<T>] {

    fn fmt(String &Vec<T>) -> [String] {
        as [self]
        self Vec.len 0 == if { "[ ]" fmt return }
        
        '[' fmt
        0 while dup self Vec.len 1 - < do {
            as [i]
            i self Vec.get Option.unwrap @ fmt
            ' '              fmt
            i 1 +
        } drop

        self Vec.last Option.unwrap @ fmt 
        ']' fmt
    }

}

impl<T> Format<Vec<T>>
requires: [Format<T>] {
    fn fmt(String Vec<T>) -> [String] {
        as [self]
        &self fmt
        &self Vec.delete
    }
}