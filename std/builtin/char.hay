fn char.as_str(&char) -> [Str] {
    1 swap cast(Str)
}

fn char.is_numeric(char: c) -> [bool] {
    '0' cast(u8) c cast(u8) <=
    '9' cast(u8) c cast(u8) >= land
}

fn char.parse_u8(char: c) -> [u8] {
    c char.is_numeric if {
        
        c cast(u8) '0' cast(u8) - 
    } else {
        0u8
        "Cannot parse u8 from non-numeric char: " print c println
        1 exit        
    }
}

impl Write<char> {
    inline fn write(char: c u64: fd) {
        &c char.as_str fd write
    }
}

impl Add<char char> {
    char: Output
    inline fn add(char: left char: right) -> [char] {
        left cast(u64) right cast(u64) + cast(char)
    }
}

impl ToString<&char> {
    inline fn to_string(&char) -> [String] {
        char.as_str String.new
    }
}

impl ToString<char> {
    inline fn to_string(char: c) -> [String] {
        &c to_string
    }
}

impl Format<char> {
    inline fn fmt(String char) -> [String] { String.push }
}

impl Format<&char> {
    inline fn fmt(String &char) -> [String] { @ String.push }
}