include "std.hay"
interface Add<A B> {
    _: Output 
    fn add(A B) -> [Output]
}

struct Point {
    pub u64: x
    pub u64: y
}

impl Add<u64 u64> {
    u64: Output
    fn add(u64 u64) -> [u64] { + }
}

impl Add<u8 u64> {
    u64: Output
    fn add(u8: left u64: right) -> [u64] {
        left cast(u64) right +
    }
}

impl Add<u64 u8> {
    u64: Output
    fn add(u64 u8) -> [u64] { cast(u64) + }
}

impl Add<Point Point> {
    Point: Output
    fn add(Point:left  Point: right) -> [Point] {
        left::x right::x +
        left::y right::y + 
        cast(Point)
    }
}

fn main() {

    1   2   add println
    1u8 2   add println
    1   2u8 add println

    3 4 cast(Point)
    1 2 cast(Point)
    add as [p]
    "Point(" print p::x print ", " print p::y print ")" println

}