include "std.hay"
include "selfhost/ir/keyword.hay"
include "selfhost/ir/literal.hay"
include "selfhost/ir/operator.hay"
include "selfhost/ir/marker.hay"

enum TokenKindTag {
    Keyword
    Operator
    Literal
    Marker
    Word
    EndOfFile
}

union TokenKindValue {
    Keyword: keyword
    Operator: operator
    Literal: literal
    Marker: marker
    Str: word
    u64: eof
}

struct TokenKind {
    TokenKindTag: tag
    TokenKindValue: value
}

fn TokenKind.fputs(TokenKind: kind u64: fd) {
    "TokenKind::" fd fputs
    kind::tag TokenKindTag::Keyword == if {
        kind::value::keyword fd Keyword.fputs
    } else kind::tag TokenKindTag::Operator == if {
        kind::value::operator fd Operator.fputs
    } else kind::tag TokenKindTag::Literal == if {
        kind::value::literal fd Literal.fputs
    } else kind::tag TokenKindTag::Marker == if {
        kind::value::marker fd Marker.fputs
    } else kind::tag TokenKindTag::Word == if {
        "Word(" fd fputs
        kind::value::word fd fputs
        ")" fd fputs
    } else kind::tag TokenKindTag::EndOfFile == if {
        "EndOfFile" fd fputs
    } else {
        "Unhandled TokenKind" putlns
        1 exit
    }
}

fn TokenKind.equals(TokenKind: t1 TokenKind: t2) -> [bool] {
    t1::tag t2::tag == if { 

        t1::tag TokenKindTag::Keyword == if {
            t1::value::keyword t2::value::keyword ==
        } else t1::tag TokenKindTag::Operator == if {
            t1::value::operator t2::value::operator ==
        } else t1::tag TokenKindTag::Literal == if {
            "Cannot compare literal tokens" putlns
            1 exit
            false
        } else t1::tag TokenKindTag::Marker == if {
            t1::value::marker t2::value::marker ==
        } else t1::tag TokenKindTag::Word == if {
            true
        } else t1::tag TokenKindTag::EndOfFile == if {
            true
        } else {
            "Unknown TokenKind" putlns
            1 exit 
            false
        }

    } else {
        false
    }
}

fn TokenKind.puts(TokenKind) {
    1 TokenKind.fputs
}

fn TokenKind.from_keyword(Keyword: value) -> [TokenKind] {
    TokenKindTag::Keyword
    value cast(TokenKindValue)
    cast(TokenKind)
}

fn TokenKind.from_operator(Operator: value) -> [TokenKind] {
    TokenKindTag::Operator
    value cast(TokenKindValue)
    cast(TokenKind)
}

fn TokenKind.from_literal(Literal: value) -> [TokenKind] {
    TokenKindTag::Literal
    value cast(TokenKindValue)
    cast(TokenKind)
}

fn TokenKind.from_marker(Marker: value) -> [TokenKind] {
    TokenKindTag::Marker
    value cast(TokenKindValue)
    cast(TokenKind)
}

fn TokenKind.from_word(Str: value) -> [TokenKind] {
    TokenKindTag::Word
    value cast(TokenKindValue)
    cast(TokenKind)
}

fn TokenKind.from_eof() -> [TokenKind] {
    TokenKindTag::EndOfFile
    0 cast(TokenKindValue)
    cast(TokenKind)
}

struct Loc {
    Str: file
    u64: row
    u64: col
}

fn Loc.fputs(Loc: loc u64: fd) {
    loc::file fd fputs 
    ":" fd fputs 
    loc::row fd fputu 
    ":" fd fputs 
    loc::col fd fputu
}

fn Loc.puts(Loc) {
    1 Loc.fputs
}

struct Token {
    TokenKind: kind
    Loc: loc
}

fn Token.fputs(Token: tok u64: fd) {
    tok::loc fd Loc.fputs
    ": " fd fputs
    tok::kind fd TokenKind.fputs 
}

fn Token.puts(Token) {
    1 Token.fputs
}

fn Token.is_kind(Token: token TokenKindTag: kind) -> [bool] {
    token::kind::tag kind == 
}

fn Token.keyword(Token: token) -> [Opt<Keyword>] {
    token TokenKindTag::Keyword Token.is_kind if  {
        token::kind::value::keyword Opt.Some
    } else {
        Opt.None::<Keyword>
    }
}

fn Token.word(Token: token) -> [Opt<Str>] {
    token TokenKindTag::Word Token.is_kind if {
        token::kind::value::word Opt.Some
    } else {
        Opt.None::<Str>
    }
}

fn Token.literal(Token: token) -> [Opt<Literal>] {
    token TokenKindTag::Literal Token.is_kind if {
        token::kind::value::literal Opt.Some
    } else {
        Opt.None::<Literal>
    }
}