include "str.hay"
include "alloc.hay"

struct File {
    u64: fd
}

fn File.exists(Str: file_name) -> [bool] {
    var u64[18]: statbuf_p
    statbuf_p @ as [statbuf]
    0 statbuf memset
    
    statbuf::data file_name::data sys_stat
}

fn File.open(u64: mode u64: flags Str: path) -> [File] {
    var u8[1024]: str_buff str_buff @ as [cstr]
    path cstr Str.to_cStr as [filepath success]
    success lnot if {
        "Failed to null terminate the path. Exiting" putlns
        1 exit
    }

    mode flags filepath::data sys_open as [fd]

    fd cast(File)
}

fn File.close(File: file) {
    file::fd sys_close drop
}

fn File.str_from_path(Str: path) -> [Str] {
    0 0 path File.open as [file]
    file File.size as [size]
    size malloc::<u8> as [buffer]
    size buffer::data file::fd sys_read drop
    file File.close

    buffer::size buffer::data cast(Str)

}

fn File.size(File: file) -> [u64] {
    var u64[18]: statbuf_p
    statbuf_p @ as [statbuf]
    0 statbuf memset
    statbuf::data file::fd sys_fstat if {
        statbuf::data 6 ptr+ cast(u64) cast(*u64) @ 
    } else {
        "Syscall `fstat` failed " putlns
        1 exit
        0
    }
}
